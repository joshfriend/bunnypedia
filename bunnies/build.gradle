apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: "androidx.navigation.safeargs.kotlin"

androidExtensions {
    experimental = true
}

kapt {
    useBuildCache = true
}

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.fueledbycaffeine.bunnypedia"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 33
        versionName "0.26"

        multiDexEnabled true
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        // Sets Java compatibility to Java 8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        preDexLibraries true
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
        quiet true
        lintConfig rootProject.file('lint.xml')

        warningsAsErrors true
        checkDependencies true
        checkTestSources true
        explainIssues false

        // We run a full lint analysis as build part in CI, so skip vital checks for assemble task.
        checkReleaseBuilds false
    }

    signingConfigs {
        release {
            def password = getPassword(defaultConfig.applicationId)
            storeFile file("$rootProject.projectDir/bunnypedia.jks")
            storePassword password
            keyAlias 'bunnypedia'
            keyPassword password
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }
        release {
            signingConfig signingConfigs.release

            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    google()
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }

    // Still get fastscroll dep from here
    //noinspection JcenterRepositoryObsolete
    jcenter()
}

dependencies {
    // https://developer.android.com/jetpack/androidx/versions/stable-channel
    // Support
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation "androidx.appcompat:appcompat:1.5.1"
    implementation "androidx.fragment:fragment-ktx:1.5.4"
    implementation "androidx.preference:preference-ktx:1.2.0"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "com.google.android.material:material:1.4.0"
    implementation 'com.google.android:flexbox:2.0.0'

    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.5.1"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.5.1"
    def navVersion = "2.5.3"
    implementation "androidx.navigation:navigation-fragment-ktx:$navVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navVersion"
    def roomVersion = "2.4.3"
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    def pagingVersion = "2.1.2"
    implementation "androidx.paging:paging-runtime-ktx:$pagingVersion"
    implementation "androidx.paging:paging-rxjava2-ktx:$pagingVersion"

    def epoxyVersion = "3.9.0"
    implementation "com.airbnb.android:epoxy:$epoxyVersion"
    kapt "com.airbnb.android:epoxy-processor:$epoxyVersion"

    // Dependency injection
    def daggerVersion = "2.44"
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    // Rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'
    def rxBindingVersion = "3.1.0"
    implementation "com.jakewharton.rxbinding3:rxbinding:$rxBindingVersion"
    implementation "com.jakewharton.rxbinding3:rxbinding-appcompat:$rxBindingVersion"

    def glideVersion = '4.13.2'
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"
    implementation "me.saket:better-link-movement-method:2"
    implementation 'com.futuremind.recyclerfastscroll:fastscroll:0.2.5'

    // Logging
    implementation 'com.jakewharton.timber:timber:5.0.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
}

def getPassword(String item) {
    if (System.getenv('CI') != null) {
        System.getenv('ANDROID_KEYSTORE_PASSWORD')
    } else {
        def stdout = new ByteArrayOutputStream()
        def stderr = new ByteArrayOutputStream()
        exec {
            commandLine 'security', '-q', 'find-generic-password', '-s', item, '-w'
            standardOutput = stdout
            errorOutput = stderr
            ignoreExitValue true
        }
        //noinspection GroovyAssignabilityCheck
        stdout.toString().trim()
    }
}

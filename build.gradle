import io.gitlab.arturbosch.detekt.Detekt
import io.gitlab.arturbosch.detekt.DetektCreateBaselineTask

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.7.22'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:2.5.3"
    }
}

plugins {
    // Variables can't be used in 'plugins' block,
    // so duplication of detekt version string is unavoidable in Gradle Groovy DSL for now.
    // Others experiencing it:  https://github.com/gradle/gradle/issues/3593
    // Mozilla decided to live with the duplication:  https://github.com/mozilla-mobile/focus-android/issues/1886
    // Possible solution that requires changing to Gradle Kotlin DSL:  https://github.com/gradle/gradle/issues/1697
    id "io.gitlab.arturbosch.detekt" version "1.23.1"
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            allWarningsAsErrors = true
            jvmTarget = JavaVersion.VERSION_1_8
        }
    }
}

subprojects {
    apply plugin: 'io.gitlab.arturbosch.detekt'

    detekt {
        buildUponDefaultConfig = true // preconfigure defaults
        allRules = false // activate all available (even unstable) rules.
        config = files("$rootProject.projectDir/detekt-config.yml")
        baseline = file("$rootProject.projectDir/detekt-baseline.xml")
    }

    // enable detekt formatting rules
    dependencies {
        detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.23.1"
        detektPlugins "io.gitlab.arturbosch.detekt:detekt-rules-style:1.23.1"
    }
    tasks.withType(Detekt).configureEach {
        jvmTarget = "1.8"
        reports {
            html.required = true
        }
    }
    tasks.withType(DetektCreateBaselineTask).configureEach {
        jvmTarget = "1.8"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}


//buildCache {
//    local {
//        directory = new File(rootDir, '.gradle/build-cache')
//        removeUnusedEntriesAfterDays = 30
//    }
//}
